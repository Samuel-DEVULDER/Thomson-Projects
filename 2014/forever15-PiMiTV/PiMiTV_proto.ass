(main)PiMiTV
***************************************
*   Participation is More important
*             Than Victory
*          (P. de Coubertin)
*
* Participation a la Forever Part 2015.
*
* - Player 3 voix (2 instru + 1 bruit)
* - Kefrens Bars
* - Musique: FA 18 interceptor theme
*
* Auteur: S.Devulder
* Date:   Fev 2014
***************************************
	
       org    $6700
       
PITCH  set    0      ; plus grave ?
SOUND  set    1      ; 0 pour mute
CPMODE set    3
* MODE de copie de ligne
* 0 => 1015   19/2 = 9.5 cycles/px
* 1 => 1022   30/5 = 6.0 cycles/px
* 2 => 1015   16/2 = 8.0 cycles/px
* 3 => 1022   34/7 = 4.8 cycles/px
* 4 => 1018   15/2 = 7.5 cycles/px
       
STATUS equ    $6019
TIMEPT equ    $6027

* 6848	
CRC    equ    $E7C1  ; ctrl port C
DDRC   equ    $E7C2  ; sens port C
PRC    equ    $E7C3  ; data port C

TCR    equ    $E7C5  ; reg cont timer
TMSB   equ    $E7C6  ; compteur timer

* 6821 musique et jeux
PRA    equ    $E7CC  ; data port A
PRB    equ    $E7CD  ; data port B
CRA    equ    $E7CE  ; ctrl port A
CRB    equ    $E7CF  ; ctrl port B

* Clavier
KTST   equ    $E809
GETC   equ    $E806

* Ecran
PUTC   equ    $E803

***************************************
* Alignement
***************************************
ALIGN  macro
       org    ((*+\0-1)/\0)*\0
       endm

***************************************
* boucle:
*      REPEAT
*      ....
*      WHILE  condition
***************************************
REPEAT macro
loop   set    *
       endm
WHILE  macro
       b\0    loop
       endm
LWHILE  macro
       lb\0    loop
       endm
	
***************************************
* variable dans code auto-modifiable
*
*      VAR1   opcode,val,label ;8 bits
*      VAR2   opcode,val,label ;16 bits
***************************************
VAR1   macro
       \0     \1
\2     set    *-1
       endm
VAR2   macro
       \0     \1
\2     set    *-2
       endm

***************************************
* Repetitions
***************************************
RPT1   macro
       \0     \1
       endm
RPT2   macro
       \0     \1
       \0     \1
       endm
RPT4   macro
       \0     \1
       \0     \1
       \0     \1
       \0     \1
       endm      
RPT8   macro
       \0     \1
       \0     \1
       \0     \1
       \0     \1
       \0     \1
       \0     \1
       \0     \1
       \0     \1
       endm       
RPT16  macro
       RPT8   \0,\1
       RPT8   \0,\1
       endm       
RPT32  macro
       RPT16  \0,\1
       RPT16  \0,\1
       endm       
RPT64  macro
       RPT32  \0,\1
       RPT32  \0,\1
       endm       
RPT128 macro
       RPT64  \0,\1
       RPT64  \0,\1
       endm       
RPT256 macro
       RPT128 \0,\1
       RPT128 \0,\1
       endm       
RPT    macro
       ifne   (\0)&256
       RPT256 \1,\2
       endc
       ifne   (\0)&128
       RPT128 \1,\2
       endc
       ifne   (\0)&64
       RPT64  \1,\2
       endc
       ifne   (\0)&32
       RPT32  \1,\2
       endc
       ifne   (\0)&16
       RPT16  \1,\2
       endc
       ifne   (\0)&8
       RPT8   \1,\2
       endc
       ifne   (\0)&4
       RPT4   \1,\2
       endc
       ifne   (\0)&2
       RPT2   \1,\2
       endc
       ifne   (\0)&1
       RPT1   \1,\2
       endc
       endm

***************************************
* message
***************************************
OUTC1  macro
       ifne   1
       fcb    \0
       else
       ldb    #\0
       jsr    PUTC
       endc
       endm
OUTC2  macro
       OUTC1  \0
       OUTC1  \1
       endm
OUTC4  macro
       OUTC1  \0
       OUTC1  \1
       OUTC1  \2
       OUTC1  \3
       endm
OUTC8  macro
       OUTC1  \0
       OUTC1  \1
       OUTC1  \2
       OUTC1  \3
       OUTC1  \4
       OUTC1  \5
       OUTC1  \6
       OUTC1  \7
       endm
FORME  macro
       OUTC2  27,$40+\0
       endm
FOND   macro
       OUTC2  27,$50+\0
       endm       
DBLCAR macro
       OUTC2  27,$4E
       endm
CLRLIN macro
       OUTC1  24
       endm
CLS    macro
       OUTC1  12
       endm       
INV    macro
       OUTC2  27,$5C
       endm
       
TEXT   macro
       CLS
       DBLCAR
       FORME  7
       FOND   1
       OUTC4  32,32,32,32
       OUTC2  32,32
       OUTC1  32
*       OUTC4  'B,'P,'T,'W
       OUTC1  '*
       OUTC4  'P,'U,'L,'S
       OUTC1  '*
*       OUTC4  'P,'I,'M,'I
*       OUTC2  'T,'V
       CLRLIN
       endm

txt
       TEXT       
len    set    *-txt+1

       includ PLAYER.ASM
       
***************************************
* Point d'entree
***************************************
ini    set    *
 
* message d'intro: le titre de la demo
       ldx    #txt
* astuce: les repetitions de code sont
* tres bien compressees par EXOMIZER.
PRT    macro
       ldb    ,x+
       jsr    PUTC
       endm
       
       RPT    len,PRT

* astuce: le dernier PUTC affiche la 
* valeur de tick (1), ce qui provoque le
* passage en memoire forme par PUTC.
* En outre X pointe alors sur SNDINT
* ce qui permet a SNDINI de s'epargner
* un LDX.

       includ SNDINI.ASM
       includ KFNINI.ASM
 
* interruptions ON       
*       andcc  #255-$50 ; int. on

       includ KFNBAR.ASM       

Song   equ    *
       includ SONG.ASM
       fcb    cZZ

       includ KFNDAT.ASM       

       end    ini

(include)SNDINI.ASM       
***************************************
* Initialisation du son
***************************************
*       orcc   #$50

*       ldx    #SNDINT ; intr timer
       stx    TIMEPT

* 8 interrupts timer pour DO 4
* (octave du LA 440)      
       ldd    #478<+PITCH
       std    TMSB
       lda    #$42
       sta    TCR
  
* Init musique et jeux
       ldd    #$043F
       clr    CRB
       stb    PRB
       sta    CRB
       stb    PRB     ; volume full

* autorisation interrupt timer       
       lda    #32*SOUND
 *      ora    STATUS
       sta    STATUS

* TO7: buzzer clavier off
*       sta    $6073
       
(include)PLAYER.ASM
***************************************
* Interruption timer
***************************************  
*       ALIGN  256
tick   fcb    1
SNDINT lda    #tick<-8
       tfr    a,dp
       setdp  tick<-8
       dec    <tick
       bne    doSnd
       ldb    #TEMPO<-PITCH
       stb    <tick

***************************************
* Commandes
* %1yxxzzzz y=canal x=octave z=note
* %01yxxxxx y=canal x=volume
* %001xxxxx x=noise
* %000xxxxx extra cmd
*     00000 TICK
*     00001 LOOP
***************************************
       VAR2   ldu,#Song,SongPt
NxtCmd lda    ,u
       ldb    ,u+
       bmi    CmdFRQ
       beq    CmdEnd
       rolb
       bmi    CmdVOL
       rolb
       bmi    CmdNSE
       ldu    #Song
       jmp    <NxtCmd
CmdEnd stu    <SongPt
doSnd  set    *       
       VAR1   ldb,#0,noise
       beq    sndnor
       lsrb
       bne    bruit
       VAR2   ldd,#$03F9,seed1
       mul
       VAR1   addd,#0,seed2
       sta    <seed2
       stb    <seed1
       coma          ; forcage carry
       rorb
bruit  stb    <noise
       VAR1   ldb,#63,vol3
       bcs    sndnor
       clrb
* son normal       
sndnor set    *
       VAR1   lda,#0,frq1  ; oscilo 1
       VAR1   adda,#0,osc1
       sta    <osc1
       bpl    sndn2
       VAR1   addb,#31,vol1
sndn2  set    *
       VAR1   lda,#0,frq2  ; oscilo 2
       VAR1   adda,#0,osc2
       sta    <osc2
       bpl    sndn3
       VAR1   addb,#31,vol2
sndn3  stb    PRB
       lda    TMSB
       rti
***************************************
* %1yxxzzzz NOTE
***************************************
CmdFRQ ldx    #tabfrq
       andb   #%1111
       ldb    b,x
       bita   #%01000000
       bne    CmdFR1
       anda   #%00110000
       beq    CmdFR2
       REPEAT
       lsrb
       suba   #%00010000
       WHILE  ne
CmdFR2 stb    <frq1
       jmp    <NxtCmd
CmdFR1 anda   #%00110000
       beq    CmdFR3
       REPEAT
       lsrb
       suba   #%00010000
       WHILE  ne
CmdFR3 stb    <frq2
       jmp    <NxtCmd
***************************************
* %01yxxxxx VOLUME
***************************************
CmdVOL anda   #%00011111
       rolb
       bmi    CmdVL0
       sta    <vol2
       jmp    <NxtCmd
CmdVL0 sta    <vol1
       jmp    <NxtCmd
***************************************
* %001xxxxx NOISE
***************************************
CmdNSE anda   #%00011111
       beq    CmdNS2
       tst    <noise
       bne    CmdNS1
       inc    <noise
       bra    CmdNS1
CmdNS2 sta    <noise
CmdNS1 sta    <vol3
       jmp    <NxtCmd      
***************************************
* Definition des notes
***************************************
* DO     set    128
* RE     set    9*DO/8
* MI     set    10*DO/8
* FA     set    4*DO/3
* SOL    set    12*DO/8
* LA     set    5*DO/3
* SI     set    15*DO/8

n0P    set    %10000000
n1P    set    %11000000

* Game temperee
nP     set    0    ; Pause
nC     set    128  ; do
nCs    set    136  ; do#
nD     set    144  ; re
nDs    set    152  ; re#
nE     set    161  ; mi
nF     set    171  ; fa
nFs    set    181  ; fa#
nG     set    192  ; sol
nGs    set    203  ; sol#
nA     set    215  ; la
nAs    set    228  ; la#
nB     set    242  ; si

       ifne   0
* Gamme naturelle (diminuee)
nCs    set    nC*16/15
nD     set    nC*9/8
nDs    set    nC*6/5
nE     set    nC*5/4
nF     set    nC*4/3
nFs    set    nC*64/45
nG     set    nC*3/2
nGs    set    nC*8/5
nA     set    nC*5/3
nAs    set    nC*9/5
nB     set    nC*15/8
       endc

       ifne   0
* Gamme naturelle (augmentee)
nCs    set    nC*25/24
nDs    set    nC*75/64
nFs    set    nC*45/32
nGs    set    nC*25/16
nAs    set    nC*225/128
       endc

tabfrq fcb    nP<-(1+PITCH)
       fcb    nC<-(1+PITCH)
       fcb    nCs<-(1+PITCH)
       fcb    nD<-(1+PITCH)
       fcb    nDs<-(1+PITCH)
       fcb    nE<-(1+PITCH)
       fcb    nF<-(1+PITCH)
       fcb    nFs<-(1+PITCH)
       fcb    nG<-(1+PITCH)
       fcb    nGs<-(1+PITCH)
       fcb    nA<-(1+PITCH)
       fcb    nAs<-(1+PITCH)
       fcb    nB<-(1+PITCH)
              
NOTE   macro
n0\0\1 equ    iOCTAV+n0P+((4-\1)<+4)
n1\0\1 equ    iOCTAV+n1P+((4-\1)<+4)
iOCTAV set    iOCTAV+1
       endm

OCTAVE macro
iOCTAV set    1
       NOTE   C,\0
       NOTE   Cs,\0
       NOTE   D,\0
       NOTE   Ds,\0
       NOTE   E,\0
       NOTE   F,\0
       NOTE   Fs,\0
       NOTE   G,\0
       NOTE   Gs,\0
       NOTE   A,\0
       NOTE   As,\0
       NOTE   B,\0
       endm

       OCTAVE 1
       OCTAVE 2
       OCTAVE 3
       OCTAVE 4
       
cTK    equ    %00000000       
cV0    equ    %01000000
cV1    equ    %01100000
cNZ    equ    %00100000
cZZ    equ    %00000001

(include)KFNBAR.ASM
***************************************
* Kefrens bar
***************************************
* A = 9.0 
* DO 
*  ' calcul d'une image 
*   CLS 
*   FOR i=0 TO 90 
*     A = A +  88.0
*     B = A + (90-i)/6.0 
*     t = 1.0+sin(A)*sin(B)
*     x = 150*t 
*     y = i*2 
*     ' Affichage d'une barre en (x,y)
*     ' jusqu'au bas de l'ecran 
*     BOXF(x,y)-(X+19,199),-7 
*     BOX(x,y)-(X+19,199),4 
*   NEXT 
* LOOP
***************************************

VIDDEB set    $4000+80*5
VIDFIN set    VIDDEB+93*80
INVPI4 set    326
TWO_PI set    1608
FOURPI set    TWO_PI*2

***************************************
* Multiplication en fixed point. Ici 
* on utilise le fait que si T(v)=v*v/4,
* alors MUL(d,u) = T(d+u) - T(d-u).
* Si T est precalcule dans une table
* (sqrtab), alors on constante que 
* la multiplication signee est reduite
* a quelques additions 16bits, ce qui
* est tres tres rapide et surtout prend
* peu d'octets.
*
* Note: pour des raisons pratique 
* d'economie d'un lslb rola pour lire
* sqrtab, l'ensemble du calcul est fait
* sur des valeurs paires (tous les
* nombres fixpt sont remplaces par leur
* doubles).
***************************************
mul_d macro
* U pointe sur U+sqrtab+1       
       comb
       coma
       leau   d,u  ; u-d
       comb
       coma
       lslb
       rola        ; 2*d+1 (car coma place la carry)
       ldd    d,u  ; u-d+2d+1=u+d
       subd   ,u
* Le resultat doit etre pair pour
* respecter la convention de parite
* des nombres fixpt.
       andb   #252    * 254 en theorie
       endm
       
mul_du macro
       leau   >sqrtab+1,u 
       mul_d
       endm       
  
/ 
* Rapide mais se compresse mal
mul_d  macro
       tfr    d,u
       ldd    >sqrtab+\0,u
       subd   >sqrtab-\0,u       
       andb   #252
       endm
/

sin_d  macro
***************************************
* Calcul du sinus. Ici pour calculer le
* sinus d'une valeur V entre -PI et PI,
* on utilise une formule approximee qui
* est largement suffisante pour la
* representation en point fixe 8.8:
*
* sin(V) = ((1-1/PI*abs(V))*4/PI)*V
*
* On a donc besoin uniquement de 3 
* multiplications ce qui est rapide.
*
* Note: pour des raison d'optim
* generale, la routine sin calcule en 
* fait reg D = 2*sin(D>>1) et le reg Y
* est trashe
***************************************
       pshs   d,u
* La constante INVPI4 vaut 4/PI en
* fixpt 8.8
       ldu    #-(INVPI4/4)*2+sqrtab+1
       tsta
       bge    *+5
       ldu    #+(INVPI4/4)*2+sqrtab+1
       
       mul_d
       adda   #2 
* On devrait avoir INVPI4, mais ca
* a cause des arrondis on a un resultat
* qui atteint 257: on poil au dessus
* de l'unite. Du coup on reduit un peu
* la constante multiplicative finale 
* pour avoir un resultat entre -255 et
* 255.
       ldu    #(INVPI4-3-26)*2+sqrtab+1
       mul_d
* y a t'il moyen d'optimiser ici???
       puls   u
       mul_du
       puls   u
       endm

/
sin_d  macro
***************************************
* Calcul du sinus. Ici pour calculer le
* sinus d'une valeur V entre -PI et PI,
* on utilise une formule approximee qui
* est largement suffisante pour la
* representation en point fixe 8.8:
*
* sin(V) = ((1-1/PI*abs(V))*4/PI)*V
*
* On a donc besoin uniquement de 3 
* multiplications ce qui est rapide.
*
* Note: pour des raison d'optim
* generale, la routine sin calcule en 
* fait reg D = 2*sin(D>>1) et le reg Y
* est trashe
***************************************
* La constante INVPI4 vaut 4/PI en
* fixpt 8.8
       ldu    #-(INVPI4/4)*2+sqrtab+1
       std    ,--s
       bge    *+5
       ldu    #+(INVPI4/4)*2+sqrtab+1
       mul_d
       adda   #2 
* On devrait avoir INVPI4, mais ca
* a cause des arrondis on a un resultat
* qui atteint 257: on poil au dessus
* de l'unite. Du coup on reduit un peu
* la constante multiplicative finale 
* pour avoir un resultat entre -255 et
* 255.
       ldu    #(INVPI4-3-26)*2+sqrtab+1
       mul_d
       puls   u
       mul_du
       endm
/

***************************************
* Affiche une ligne de sprite en ram
* video. Le registre U pointe dans la
* zone video, et X dans les donnes
* des sprites. En sortie X pointe sur 
* la valeur d'entree + 8, c'est a dire
* qu'il est pret pour afficher la ligne
* suivante.
*
* Note sur l'organisation memoire:
* ================================
* En fait on affiche 9 valeurs + masque
* ce qui devrait prendre 18 octets par
* lignes. Ca fait beaucoup trop pour 
* la contrainte de taille. Si on 
* remarque que seuls les octets de
* debut et fin ont besoin d'un masque
* car les donnees internes au segment
* sont simplement copiees, on peut
* reduire la taille a 9 octets/ligne.
*
* dat:127,255,255,255,127,255,255,254,0
*
* Un alignement sur 8 serait
* souhaitable pour le calcul facile
* par shift. On remarque que l'octet 3
* du motif est redondant avec l'octet
* 2. On peut donc le supprimer. Ensuite
* pour accelerer l'affichage il faut
* grouper les paires de valeurs
* consecutives ensemble pour pouvoir
* faire un "std" au lieu d'un sta/stb
* (on gagne quelques cycles, mais
* surtout plusieurs octets). On aboutit
* alors aux donnees suivantes pour les
* deux lignes en 16 octets (au lieu de
* 36):
* cmpr : 0,127,255,255,127,254,0,255
*        0,255,255,254,255,252,1,254
***************************************
drwsp1 macro
       lda    ,x+
       anda   ,u
       ora    ,x+
       sta    ,u
       ldd    ,x++
       std    1,u
       std    5,u
       ldb    ,x+
       std    3,u
       lda    ,x+
       ldb    1,x
       andb   8,u
       orb    ,x++
       std    7,u
       endm
	
***************************************
* Addition modulo 2PI. Ajoute "B" (non
* signe) a la valeur 16bits pointee par
* le registre X qui est incremente de
* deux pour passer a la valeur suivante
* automatiquement.
* En entree A doit valoir 0
***************************************
addmod macro
       addd   ,x
       cmpd   #TWO_PI/2*2
       ble    *+5
       subd   #TWO_PI*2
       std    ,x++
       endm
       
kfnbar set    *       
***************************************
* Calcul d'une image
***************************************
       ldu    #VIDDEB
       ldx    #ANGLE_A
       ldd    ,x++    * ANGLE_B=ANGLE_A
       std    ,x++
***************************************
* Y contient un pointeur sur le debut
* de la ligne courante. L'utilisation
* d'un LEA est plus efficace qu'un TFR.
*
* Les autres regs (D,X,U) peuvent etre
* ecrases.
***************************************
       REPEAT
       leay   ,u

***************************************
* Incrementation des angles:
*       ANGLE_A += 9+43
*       ANGLE_B += 9
* En entree X pointe sur ANGLE_A, en
* sortie D contient ANGLE_B.
***************************************
       ldx    #ANGLE_A
       ldd    #(9+43)*2
       addmod
       ldd    #(9)*2
       addmod
       
***************************************
* Calcul de l'absice pour la ligne 
* courante:
* D = 2*(1+sin(ANGLE_A)*sin(ANGLE_B))
*
* En entree D en entree contient
* ANGLE_B et X pointe sur ANGLE_A. Le
* registre U est trashe.
***************************************
       sin_d        * sin(B)
       
       tfr    d,u
       ldd    -4,x  * A
       sin_d        * sin(A)
       mul_du       * 2*sin(A)*sin(B)
       adda   #2    * 2+2*sin(A)*sin(B)
*       andb   #252	* parite
/
       std    ,x
       ldd    -4,x
       sin_d
       ldu    ,x
       mul_du
       adda   #2
/       

***************************************
* Conversion Fixed point (D) vers
* memoire video (U). 
*
* Cela revient a multiplier par 8 la
* valeur en fixed point et prendre la
* partie entiere.
*
* En entree Y pointe sur le début de
* ligne.
***************************************
       lslb
       rola
       lslb
       rola
       lslb
       rola
       leau   a,y

***************************************
* recopie les 2 lignes au dessus du 
* pointeur Y. X et A sont trashes.
***************************************
       ifeq   CPMODE-0
       lda    #-78
       ldx    a,y    * <-+
       adda   #80    *   :
       stx    a,y    *   :
       adda   #-78   *   :
       bne    *-8    * --+
       endc

       ifeq   CPMODE-1
       pshs   x,y,a,u
       leau  -78,y
       
       pulu   a,x,y
       leau   80,u
       pshu   a,x,y
       leau   -75,u
       
       pulu   a,x,y
       leau   80,u
       pshu   a,x,y
       leau   -75,u
       
       pulu   a,x,y
       leau   80,u
       pshu   a,x,y
       leau   -75,u
       
       pulu   a,x,y
       leau   80,u
       pshu   a,x,y
       leau   -75,u
       
       pulu   a,x,y
       leau   80,u
       pshu   a,x,y
       leau   -75,u
       
       pulu   a,x,y
       leau   80,u
       pshu   a,x,y
       leau   -75,u
       
       pulu   a,x,y
       leau   80,u
       pshu   a,x,y
       leau   -75,u
       
       pulu   a,x,y
       leau   80,u
       pshu   a,x,y
       leau   -75,u
       
       pulu   a,x,y
       leau   80,u
       pshu   a,x,y
       leau   -75,u
       
       pulu   a,x,y
       leau   80,u
       pshu   a,x,y
       leau   -75,u
       
       pulu   a,x,y
       leau   80,u
       pshu   a,x,y
       leau   -75,u
       
       pulu   a,x,y
       leau   80,u
       pshu   a,x,y
       leau   -75,u
       
       pulu   a,x,y
       leau   80,u
       pshu   a,x,y
       leau   -75,u
       
       pulu   a,x,y
       leau   80,u
       pshu   a,x,y
       leau   -75,u
       
       pulu   a,x,y
       leau   80,u
       pshu   a,x,y
       
       puls   x,y,a,u
       endc

       ifeq   CPMODE-2
       lda    #-78

       ldx    a,y
       adda   #80
       stx    a,y
       adda   #-78

       ldx    a,y
       adda   #80
       stx    a,y
       adda   #-78

       ldx    a,y
       adda   #80
       stx    a,y
       adda   #-78

       ldx    a,y
       adda   #80
       stx    a,y
       adda   #-78

       ldx    a,y
       adda   #80
       stx    a,y
       adda   #-78

       ldx    a,y
       adda   #80
       stx    a,y
       adda   #-78

       ldx    a,y
       adda   #80
       stx    a,y
       adda   #-78

       ldx    a,y
       adda   #80
       stx    a,y
       adda   #-78

       ldx    a,y
       adda   #80
       stx    a,y
       adda   #-78

       ldx    a,y
       adda   #80
       stx    a,y
       adda   #-78

       ldx    a,y
       adda   #80
       stx    a,y
       adda   #-78

       ldx    a,y
       adda   #80
       stx    a,y
       adda   #-78

       ldx    a,y
       adda   #80
       stx    a,y
       adda   #-78

       ldx    a,y
       adda   #80
       stx    a,y
       adda   #-78

       ldx    a,y
       adda   #80
       stx    a,y
       adda   #-78

       ldx    a,y
       adda   #80
       stx    a,y
       adda   #-78

       ldx    a,y
       adda   #80
       stx    a,y
       adda   #-78

       ldx    a,y
       adda   #80
       stx    a,y
       adda   #-78

       ldx    a,y
       adda   #80
       stx    a,y
       adda   #-78

       ldx    a,y
       adda   #80
       stx    a,y
       adda   #-78

       ldx    a,y
       adda   #80
       stx    a,y
       adda   #-78

       ldx    a,y
       adda   #80
       stx    a,y
       adda   #-78

       ldx    a,y
       adda   #80
       stx    a,y
       adda   #-78

       ldx    a,y
       adda   #80
       stx    a,y
       adda   #-78

       ldx    a,y
       adda   #80
       stx    a,y
       adda   #-78

       ldx    a,y
       adda   #80
       stx    a,y
       adda   #-78

       ldx    a,y
       adda   #80
       stx    a,y
       adda   #-78

       ldx    a,y
       adda   #80
       stx    a,y
       adda   #-78

       ldx    a,y
       adda   #80
       stx    a,y
       adda   #-78

       ldx    a,y
       adda   #80
       stx    a,y
       adda   #-78

       ldx    a,y
       adda   #80
       stx    a,y
       adda   #-78

       ldx    a,y
       adda   #80
       stx    a,y
       adda   #-78

       ldx    a,y
       adda   #80
       stx    a,y
       adda   #-78

       ldx    a,y
       adda   #80
       stx    a,y
       adda   #-78

       ldx    a,y
       adda   #80
       stx    a,y
       adda   #-78

       ldx    a,y
       adda   #80
       stx    a,y
       adda   #-78

       ldx    a,y
       adda   #80
       stx    a,y
       adda   #-78

       ldx    a,y
       adda   #80
       stx    a,y
*       adda   #-78

       endc

       ifeq   CPMODE-3
       pshs   b,y,u
       leau   -78,y
       
       pulu   d,x,y,dp
       leau   80,u
       pshu   d,x,y,dp
       leau   -73,u
       
       pulu   d,x,y,dp
       leau   80,u
       pshu   d,x,y,dp
       leau   -73,u
       
       pulu   d,x,y,dp
       leau   80,u
       pshu   d,x,y,dp
       leau   -73,u
       
       pulu   d,x,y,dp
       leau   80,u
       pshu   d,x,y,dp
       leau   -73,u
       
       pulu   d,x,y,dp
       leau   80,u
       pshu   d,x,y,dp
       leau   -73,u
       
       pulu   d,x,y,dp
       leau   80,u
       pshu   d,x,y,dp
       leau   -73,u
       
       pulu   d,x,y,dp
       leau   80,u
       pshu   d,x,y,dp
       leau   -73,u
       
       pulu   d,x,y,dp
       leau   80,u
       pshu   d,x,y,dp
       leau   -73,u
       
       pulu   d,x,y,dp
       leau   80,u
       pshu   d,x,y,dp
       leau   -73,u
       
       pulu   d,x,y,dp
       leau   80,u
       pshu   d,x,y,dp
       leau   -73,u
       
       pulu   d,x,y,dp
       leau   80,u
       pshu   d,x,y,dp
       
       puls   b,y,u
       endc
       
       ifeq   CPMODE-4
       leau   <-80,y
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       pulu   x
       stx    ,y++
       leay   <-80,y
       leau   a,y
       endc

***************************************
* Mise a jour du pointeur de sprite (X)
* a partir de la valeur fixed point.
* En fait comme seul B est utilisé, 
* la partie Haute du fixed-point peut
* etre trashee par cpline.
***************************************
       ldx    #sprdat
       lsrb
       abx

***************************************
* affiche les deux lignes d'un sprite.
***************************************
       drwsp1
       leau   <40,u
       drwsp1

       leau   <80,y
       cmpu   #VIDFIN
       LWHILE  lo
       jmp    kfnbar
       
(include)KFNINI.ASM
***************************************
* Routine d'initialisation des sprites.
* Cela revient a partir de l'amorce des
* sprites et  decaler tous les masques
* de 1 bits fois fois d'affile. 
*
* Comme la routine a executer après 
* a besoin d'avoir un registre X
* pointant sur "sqrtab", on peut lui
* epargner un ldx #sqrtab en
* poursuivant les decalages jusqu'a 
* sqrtab. Cependant on ne peut faire
* de cmpx #sqrtab, car cela detruit le
* bit C du CCR qu'il est important de
* preserver dans ce code de decalage 
* memoire. Ici on s'en sort
* astucieusement en remplacant le cmpx
* par un leay -sqrtab,x qui positionne
* y a 0 (donc Z a 1) quand x atteind la
* valeur sqrtab.
***************************************
       ldx    #sprite

rorspr macro
       lda    ,x+
       rora
       sta    15,x
       endm
       
*      RPT    (1+sqrtab-sprite),rorspr

       ifne   1
       REPEAT
       rorspr
       leay   >0-sqrtab,x
       WHILE  ne
       else
       RPT    16*8,rorspr
       ldx    #sqrtab
       endc

***************************************
* Initialisation tableau des x*x/4 pour
* la multiplication signee rapide.
*
* En principe il faudrait utiliser
* une multiplication 16x16->32bits,
* mais cela prend bien trop de code.
*
* Ici on utilise le fait que la suite
* des carres peut etre obtenue en
* partant de 1 et en ajoutant une
* valeur qui est incrementee de 2 a
* chaque fois: 
*                 A=0 =0*0, B=1
* A=A+B, B=B+2 -> A=1 =1*1, B=3
* A=A+B, B=B+2 -> A=4 =2*2, B=5
* A=A+B, B=B+2 -> A=9 =3*3, B=7
* A=A+B, B=B+2 -> A=16=4*4, B=9
* A=A+B, B=B+2 -> A=25=5*5, B=11
* A=A+B, B=B+2 -> A=36=6*6, B=13
* etc.
*
* Pas mal, non? Aucun MUL utilise.
* Uniquement des additions. Et
* pourtant on calcule bien une table
* de carres.
*
* On peut optimiser le calcul et voir
* que puisque x est entre -2PI et 2PI,
* on aurait seulement besoin d'un
* calcul avec un resultat "A" sur 24
* bits dont on ne conservera que les
* 16 premiers bits de poids fort et
* que "B" peut rester sur 16 bits. "A"
* sera donc stocke en pile, et "B"
* sera represente par le registre U.
* L'accumulateur D contiendra les
* valeurs intermédiaires ainsi que les
* 16 bits de poids fort a ecrire en 
* memoire.
*
* Autre optim: dans le reste du code
* pour economiser des divisions et
* des multiplications par 2 on decide
* de ne travailler qu'avec des nombres
* pairs (tout est multiplie par 2). 
* Ainsi on ne stockera pas x*x/4, mais
* x*x/2 en economisant ainsi les 2
* octets du couple "asra; rorb".
*
* Note: en entree X est suppose pointer
* sur sqrtab et en sortie S reste
* decale de 3 octets par rapport a sa
* valeur d'entree.
***************************************
       leay   2,x	! a cause ,--y
       clra
       clrb
       tfr    d,u
       pshs   b,u
       leau   1,u
       REPEAT
* calcul de x*x/2
       lsra
       rorb
* on recopie x*x/2 dans les deux
* directions (pour les x>0 et x<0)
       std    ,x++
       std    ,--y
* B=B+2
       tfr    u,d
       leau   2,u
* A=A+B
       addd   1,s
       std    1,s
* reg "D" contient x*x
       ldd    ,s
       adca   #0
       sta    ,s
       cmpx   #sqrfin+2
       WHILE  ne
       
(include)KFNDAT.ASM
***************************************
* Tableau des angles. Il doit y en
* avoir deux: A et B qui se suivent en
* memoire a cause de la routine
* "addmod". 
***************************************
ANGLE_A
       fdb    696*2
* la version compressee avec ANGLE_B
* deborde. Mais comme sprite est
* inutilise après l'init des sprdat,
* on peut utiliser sprite en lieu et
* place de ANGLE_B.
*ANGLE_B
*       fdb    696*2

***************************************
* Ammorce pour les sprites. Avec un
* choix astucieux des motifs on peut
* remplacer le sprite de 8 octets
* de large (donc 10 octets au total a
* cause des masques) par 8 morceaux
* independants que la routine "drsprit"
* affichera correctement.
***************************************
sprite
       fcb    1,255     * 1ere ligne
       fcb    255,255
       fcb    255,254
       fcb    1,254

       fcb    0,255     * 2eme ligne
       fcb    255,254
       fcb    255,252
       fcb    1,255
	
***************************************
* Donnees pour les sprites, ils
* representent les 8 positions decalees
* du sprite.
***************************************
sprdat rmb    16*8+16
sprfin set    *
***************************************
* Tableau des carres: sqrtab contient
* les valeurs de x*x/4 pour x entre
* -2PI et 2PI. Il faut donc reserver 2
* fois 4PI octets.
***************************************
       rmb    FOURPI
sqrtab rmb    FOURPI
sqrfin set    *
(include)SONG.ASM
* SONG content will be added by Makefile
