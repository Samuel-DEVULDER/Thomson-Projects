(main)ELVISLIV
***************************************
* Compo pour la ForEver Party 14
* Theme: Elvis Lives!
*
* Idee: slide show
* Categorie: demo
* Auteur: Samuel Devulder, Mars 2013
***************************************
	
       org    $9700
       
DBGD7  set    0
DBGEXO set    0

***************************************
* boucle:
*      REPEAT
*      ....
*      WHILE  condition
***************************************
REPEAT macro
loop   set    *
       endm
WHILE  macro
       b\0    loop
       endm
	
***************************************
* variable dans code auto-modifiable
*
*      VAR1   opcode,val,label ;8 bits
*      VAR2   opcode,val,label ;16 bits
***************************************
VAR1   macro
       \0     \1
\2     set    *-1
       endm
VAR2   macro
       \0     \1
\2     set    *-2
       endm

TIMEPT equ    $6027
STATUS equ    $6019

***************************************
* Alignement sur une page
***************************************
DPALGN macro
ALIGNX set    *-ALIGNX
       ifne    \0
       org    ((\0+255)<-8<8)+(*-\0)
       else
       org    (*+255)<-8<8
       endc
ALIGNX set    *-ALIGNX
       endm
ALIGNX set    0
       
       
INI         
ini    leas   *-1,pcr
       jsr    initmr
       jsr    intro
lp     jsr    alldrv
       bra    lp
**************************************
* Scan tous les lecteur et appelle
* allcat
**************************************
       setdp  $60
alldrv pshs   dp,d
       ldd    #$60
       tfr    b,dp
       REPEAT
       sta    <DKDRV
       clr    <DKFAT+1
       clr    <DKCAT+1
       ldb    #$10     ; et pas 10
       stb    <DKOPC
       jsr    DKCOM       
       ldb    #1
       stb    <DKOPC
       jsr    DKCOM
       bcs    nxtdrv 
       clr    <DKFAT+1
       clr    <DKCAT+1
       ldb    #$10
       stb    <DKOPC
       jsr    DKCOM       
       bsr    allcat
nxtdrv inca
       cmpa   #4
       WHILE  ne
       puls   dp,d,pc
***************************************
* Appelle "affiche" sur tous les
* fichiers du catalogue du disk
* courant.
***************************************
allcat pshs   d,x
       lda    #3
       REPEAT
       sta    <DKSEC
       ldx    #20
       stx    <DKTRK
       ldx    #iocat
       stx    <DKBUF
       ldb    #2
       stb    <DKOPC
       jsr    DKCOM
       bcs    endcat
allfil ldb    ,x
       beq    nxtfil
       cmpb   #-1
       beq    endcat
       ldb    8,x
       andb   #255-32  ; toupper
       cmpb   #$58     ; X
       bne    nxtfil
       ldb    9,x
       andb   #255-32  ; toupper
       cmpb   #$4D     ; M
       bne    nxtfil
       ldb    10,x
       andb   #255-32  ; toupper
       cmpb   #$50     ; P
       bne    nxtfil
CALL   pshs   a
       clra
       clrb
       std    timer   ; timer=0
       jsr    affiche

* attente
wait   jsr    $E806
       bcc    notkbd
       cmpb   #3
       bne    notbrk
       jmp    [$FFFE]
notbrk cmpb   #32
       beq    timout 
notkbd ldd    timer
       cmpd   #150  ; 15 secs
       blo    wait
timout puls   a
nxtfil leax   32,x
       cmpx   #iocat+256
       bne    allfil
       inca
       cmpa   #17
       WHILE  NE
endcat puls   d,x,pc

**************************************
* interruption timer
TCR    equ    $E7C5  ; reg cont timer
TMSB   equ    $E7C6  ; compteur timer
initmr ldd    #tmrint
       std    TIMEPT
       lda    STATUS
       ora    #32
       sta    STATUS
       ldd    #12499   ; init timer
       std    TMSB
       lda    #$46
       sta    TCR
       andcc  #255-$50
       rts
tmrint ldd    timer
       addd   #1
       std    timer
       jmp    $E830
timer  fdb    0       
       setdp  0
       
       includ INTRO
       includ DISK
       includ EXO
**************************************
* buffers
**************************************
biba   rmb    156
iofat  rmb    256     ; fat
iobuf  rmb    256     ; buffer io
iocat  rmb    256     ; catalogue

RAMA   rmb    8000
RAMB   rmb    8000

LAST   set    *
       end    ini
(include)INTRO.ASM    
       DPALGN 0
***************************************
* Intro
***************************************
intro  ldx    #intdat  ; ecran noir
       REPEAT
       ldb    ,x+
       jsr    $E803
       WHILE  ne
       clr    DKDRV    ; drive 0
       ldb    #48      ; charge les intro
       stb    4,x
       jsr    d7open
       ldu    #RAMA
       jsr    decomp
       incb
       stb    4,x
       jsr    d7open
       ldu    #RAMB
       jsr    decomp
       
       ldu    #RAMA
       jsr    appear
       ldu    #RAMB
       jsr    appear
       rts
intdat fcb    $1B,$40  ; forme=7	
       fcb    $1B,$50  ; fond=0
       fcb    $1B,$60  ; tour=0
       fcb    $14      ; curseur off
       fcb    $c       ; cls
       fcb    $1B,$47  ; forme=7
       fcb    $c
       fcb    $1B,$68  ; ecriture forme seule
       fcb    0
       fcc    /INTR0   XXX/
***************************************
* fait apparaitre progressivement
* l'image point<E9>e par U dans la ram
* video
***************************************
appear lda   #63
       REPEAT
       bsr    dith
       bsr    disp
       deca
       WHILE  ge
       pshs   x,b    ; pause
       ldb    #4
       ldx    #0
       REPEAT
       leax   -1,x
       WHILE  ne
       decb
       WHILE  ne
       puls   x,b
       lda    #63
       REPEAT
       bsr    dith
       bsr    disp
       deca
       WHILE  ge
       rts

disp   pshs   d,x,y,u
       ldb    #7
       subb   dith3a+1
       ldx    #mask
       abx
       lda    #40
       mul
       leau   d,u
       adda   #$40
       tfr    d,y
       lda    ,x
       sta    disp5+3
       sta    disp5+5
       leax   40,y
       stx    disp6+1
       leax   ,y
disp5  pulu   d
       anda   #0
       andb   #0
       std    ,x++
disp6  cmpx   #0
       bne    disp5
       leau   40*7,u
       leax   40*8,x
       stx    disp6+1
       leax   -40,x
       cmpx   #$5F40
       bls    disp5
       puls   d,x,y,u,pc

mask   fcb    0,0,0,0,0,0,0,0

dith   pshs   d,x,u
       ldx    #dith3a+1
       ldu    #mask
       clrb
       lsla
       anda   #$AA
       eora   ,s
       lsra
       rolb
       clr    ,x
       lsra
       rol    ,x
       lsra
       rolb
       lsra
       rol    ,x
       lsra
       rolb
       lsra
       rol    ,x
       clr    4,x
       coma
dith2  rol    4,x
       decb
       bge    dith2
       ldb    #7
dith3  lda    ,u
dith3a cmpb   #00
       bne    dith4
       eora   #1
dith4  sta    ,u+
       decb
       bge    dith3
       puls   d,x,u,pc       
***************************************
* Affiche le fichier pointe par X
***************************************
affiche
       pshs   d,x,y,u
       
       jsr    d7open
       
       ldu    #RAMA
       jsr    decomp
       ldu    #RAMB
       jsr    decomp
   
       lda    $E7C3
       ora    #1
       sta    $E7C3
   
       ldx    #$0000
       REPEAT
       bsr    cpyline
       leax   8*40,x
       cmpx   #$1F40
       WHILE  lo
       ldy    #1000
       leay   -1,y
       bne    *-2
       leax   -(200-1)*40,x
       cmpx   #8*40
       WHILE  ne
       
       puls   d,x,y,u,pc

cpyline
       pshs   dp,d,x,y,u,cc
       orcc   #$50       
       setdp  cpylin<-8
       ldb    #cpylin<-8
       tfr    b,dp
       leau   >RAMA,x
       leax   >$4000,x
       bsr    cpylin       
       leau   >RAMB-RAMA,u
       dec    >$E7C3
       bsr    cpylin
       inc    >$E7C3
       puls   dp,d,x,y,u,cc,pc
cpylin pshs   x,u
       sts    <BAKSP
       stx    <VIDPTR1
       
*       clrb
*       decb
*       bne   *-1

       pulu   d,x,y,s
       stu    <RAMPTR1
       VAR2   ldu,#0,VIDPTR1
       leau   8,u
       stu    <VIDPTR2
       pshu   d,x,y,s
       
       VAR2   ldu,#0,RAMPTR1
       pulu   d,x,y,s
       stu    <RAMPTR2
       VAR2   ldu,#0,VIDPTR2
       leau   8,u
       stu    <VIDPTR3
       pshu   d,x,y,s
       
       VAR2   ldu,#0,RAMPTR2
       pulu   d,x,y,s
       stu    <RAMPTR3
       VAR2   ldu,#0,VIDPTR3
       leau   8,u
       stu    <VIDPTR4
       pshu   d,x,y,s
       
       VAR2   ldu,#0,RAMPTR3
       pulu   d,x,y,s
       stu    <RAMPTR4
       VAR2   ldu,#0,VIDPTR4
       leau   8,u
       stu    <VIDPTR5
       pshu   d,x,y,s
       
       VAR2   ldu,#0,RAMPTR4
       pulu   d,x,y,s
       VAR2   ldu,#0,VIDPTR5
       leau   8,u
       pshu   d,x,y,s

       VAR2   lds,#0,BAKSP
       puls   x,u,pc 
       setdp 0
(include)DISK
DKOPC  equ    $6048   ;operation d7
DKDRV  equ    $6049   ;lecteur
DKTRK  equ    $604A   ;piste (2 ocets)
DKTRKL equ    DKTRK+1
DKSEC  equ    $604C   ;secteur
DKSTA  equ    $604E   ;status d7
DKBUF  equ    $604F   ;buf i/o
DKCAT  equ    $60E9   ;prt catalogue
DKNAM  equ    $60E7   ;nom fichier
DKTYP  equ    $60EB   ;type fichier (2octets)
DKFAT  equ    $60ED   ;ptr fat
DKMOD  equ    $60F0   ;mode d'acces
DKFIN  equ    $60F3   ;dernier bloc
DKBLK  equ    $60F6   ;1er bloc fichier
DKTDS  equ    $60F7   ;nb octets dern sect (2octets)
DKIFA  equ    $60F9   ;secteur fat fichier
LECFA  equ    $E00D   ;lecture fat
RECFI  equ    $E010   ;recherche fichier
CONVB  equ    $E01F   ;converison bloc
DKCOM  equ    $E004   ;$E82A   ;commande d7

**************************************
* Ouverture d'un fichier D7
* U = nom fichier (1+8+3)
* Sortie: Z=0 si erreur
**************************************
       setdp  $60
d7open pshs   d,x,dp
       bsr    page0

*       ldb    ,x+     ; lecteur
*       subb   #'0
*       stb    <DKDRV
       stx    <DKNAM  ; nom

       ldd    #iofat  ; fat
       std    <DKFAT
       inca           ; iocat
       std    <DKCAT

       ldd    #$0200
       std    <DKTYP  ; binaire
       stb    <DKSTA  ; clr err
* recherche fat
       jsr    LECFA
       jsr    d7chk

* recherche fichier
       lda    #1
       sta    <DKMOD  ; acces lecture
       jsr    RECFI   ; rech. fichier
       ldb    <DKIFA
       subb   #1      ; C=1 si 60F9=0
       bsr    d7chk
	
       ldd    #load0
       std    d7redir
       puls   d,x,dp,pc
page0  ldb    #$60
       tfr    b,dp
       rts
***************************************
* Fait clignoter le tour en attente
* de ctrl-c.
***************************************
guru   bsr    page0
       ldu    #$E803  ; affiche
       ldx    #errtxt ; guru meditation
       bsr    puts
       lda    <DKSTA
       bsr    putx
       bsr    puts
       lda    ,s
       bsr    putx
       lda    1,s
       bsr    putx
       ldb    <DKDRV
       addb   1,x
       stb    1,x
       bsr    puts
       lda    #11
       ldx    <DKNAM
       REPEAT
       ldb    ,x+
       jsr    ,u
       deca
       WHILE  ne
       lda    #$61
clign  ldb    #$1B
       jsr    ,u
       tfr    a,b
       eora   #1
       jsr    ,u
       ldx    #2000
       REPEAT
       jsr    3,u
       cmpb   #3
       beq    reset
       leax   -1,x
       WHILE  ne
       bra    clign
reset  jmp    [$FFFE]
puts   set    *      
       REPEAT
       ldb    ,x+
       jsr    ,u
       WHILE  ne
       rts
putx   sta    ,-s
       lsra
       lsra
       lsra
       lsra
       bsr    hex
       lda    #15
       anda   ,s+
hex    adda   #$90
       daa
       adca   #$40
       daa
       tfr    a,b
       jmp    ,u
d7chk  bcs    guru    ; erreur?
       rts            ; non => retour       
**************************************
* Lecture d'un bit
* Retour: C = 0 ou 1
*         Z = 0 si erreur
* (positionne ici pour avoir des
* sauts courts)
**************************************
* Tous les regs sont sauves car les
* differentes versions de rom
* n'ecrasent pas les memes regs.
d7read pshs   b,x,y,u,dp,cc
       VAR2   jmp,guru,d7redir       

* chargement 1er bloc
** Que vaut A ?
* Convertit numero de bloc
load0  bsr    page0
       jsr    CONVB   ; convertit bloc
       stb    <DKTRKL ; init piste
       ldx    <DKFAT  ; ptr FAT
       ldb    <DKBLK  ; bloc courant
       incb           ;   bloc
       ldb    b,x     ;   suivant
       stb    <DKBLK  ; sauve bloc
       cmpb   #$C0    ; si dernier bloc,
       bhi    load1   ; recupere taille
       ldb    #8      ; 8 sect a charger
load1  andb   #15     ; isole taille bloc
       sta    <DKSEC  ; sauv 1er bloc
       addb   <DKSEC  ; ajoute depart bloc
       decb           ; ajuste dern bloc
       stb    <DKFIN  ; sauv dern bloc
* charge les secteurs
load2  jsr    DKCOM
       bsr    d7chk
       lda    <DKSEC  ; si sect pas
       cmpa   <DKFIN  ; secteur max,
       bne    load3   ; passe
       lda    <DKBLK  ; si dernier
       cmpa   #$C0    ; secteur du
       bhi    load4   ; fichier,sort
       bsr    transf
       bra    load0
load3  bsr    transf
       inc    <DKSEC  ; secteur +1
       bra    load2
* chargement dernier secteur et sortie
load4  ldd    <DKTDS  ; taille dern sect
       beq    load5   ; si 0, sort
       bsr    tfr0
load5  ldx    #guru
       stx    d7redir
       puls   b,x,y,u,dp,cc,pc
* transfert des donnees
transf ldb    $6058   ; densite
       comb
       orb    #$80
tfr0   ldx    <DKBUF  ; ptr buf sect
       stx    tfrptr
       abx
       stx    tfrmax
       puls   x
       stx    tfrrts  
       ldx    #tfr2
       stx    d7redir
       jmp    ,x       
tfr1   set    *
       lda    ,x+
       stx    tfrptr
       
       ifne   DBGD7
       pshs   d,u
       ldu    #$E803
       jsr    putx
       ldb    #32
       jsr    ,u
       VAR2   ldd,#0,CPT
       addd   #1
       std    CPT
       jsr    putx
       lda    CPT+1
       jsr    putx
       ldb    #32
       jsr    ,u
       puls   d,u
       endc

       puls   b,x,y,u,dp,cc,pc
tfr2   set    *       
       VAR2   ldx,#0,tfrptr
       VAR2   cmpx,#0,tfrmax
       bne    tfr1
*       bls    tfr1
       jsr    page0
       VAR2   jmp,guru,tfrrts
errtxt fcb    $7
       fcb    $1B,$41  ; forme=1      
       fcc    /Guru Meditation #/
       fcb    0
       fcc    / $/
       fcb    0
       fcc    / 0:/
       fcb    0
       setdp  0
(include)EXO       
* Values used in the switch (index)
tab1   fcb    4,2,4
       fcb    16,48,32

       ifne  DBGEXO
get4   pshs   d,u,cc
       ldu    #$E803
       jsr    putx
       lda    2,s
       jsr    putx
       ldb    #32
       jsr    ,u
       lda    bitbuf
       jsr    putx
       ldb    #32
       jsr    ,u
       puls   d,u,cc,pc
       endc

       DPALGN 0
***************************************
* Decompresse le fichier XMP dans
* la mémoire pointee par U
***************************************
decomp pshs   d,u,x,y,dp
       setdp  *<-8
       ldb    #*<-8
       tfr    b,dp

       ldu    #biba
 
*       jsr    d7read
*       stb    <bitbuf
       clrb
       stb    <bitbuf
nxt    clra
       pshs   a,b
       bitb   #$0f
       bne    skp
       ldx    #1
skp    ldb    #4
       jsr    <getbits
       stb    ,u+
       comb
roll   rol    ,s
       rola
       incb
       bmi    roll
       ldb    ,s
       stx    ,u++
       leax   d,x
       puls   a,b
       incb
       cmpb   #52
       bne    nxt

***************************************
* decompression
***************************************
go     ldu    7,s
       stu    <STARTu
       stu    <STARTx
       leau   8000,u
       stu    <STOPx
       leau   39,u
mloop  ldb    #1
       jsr    <getbits
       bne    cpy
       stb    <idx
       fcb    $8c
rbl    inc    <idx
       incb
       jsr    <getbits
       beq    rbl

       VAR1   ldb,#0,idx
       cmpb   #$10
       beq    fin
       blo    coffs
       decb
       jsr    <getbits
***************************************
* Copy litteral
***************************************
cpy    tfr    d,y
cpyl   jsr    d7read
       jsr    <dec_u
       sta    ,u
       leay   -1,y
       bne    cpyl
       bra    mloop
fin    puls   d,u,x,y,dp,pc
***************************************
* Compte length
***************************************
coffs  jsr    <cook
       pshs   d
       ldx    #tab1
       cmpd   #$03
       bhs    scof
       abx
scof   jsr    <getbix
       addb   3,x
       jsr    <cook
       std    <offs
       puls   y

***************************************
* Copy non litteral
***************************************
cpy1   jsr    <dec_u
       leax   ,u
       VAR2   ldd,#0,offs
***************************************
* X=X+D en mode colonne
***************************************
       REPEAT
       leax   1,x
       subd   #200
       WHILE  hs
       leax   -1,x
       addd   #200
       lda    #40
       mul
       leax   d,x
       VAR2   cmpx,#0,STOPx
       blo    cpy2
       leax   1-8000,x
cpy2   lda    ,x
       sta    ,u
       leay   -1,y
       beq    mloop
       jsr    <dec_u
       jsr    <dec_x
       bra    cpy2
***************************************
* getbits  : get 0 to 16 bits from 
*            input stream
* Input    : B = bit count, U points 
*            to input buffer
* Output   : D = bits
* Modifies : D.
***************************************
getbix ldb    ,x
getbits 
       clr    ,-s
       clr    ,-s
       VAR1   lda,#0,bitbuf
       bra    get3
get1   jsr    d7read
get2   rora
       beq    get1
       rol    1,s
       rol    ,s
get3   decb
       bpl    get2
       sta    bitbuf
       ldd    ,s++
       ifne  DBGEXO
       jsr   get4
       endc
       rts
***************************************
* U=U-1 en mode colonne
***************************************
dec_u  leau   -40,u
       VAR2   cmpu,#0,STARTu
       bhs    dec_u1
       leau   8000-1,u
dec_u1 rts
***************************************
* X=X-1 en mode colonne
***************************************
dec_x  leax   -40,x
       VAR2   cmpx,#0,STARTx
       bhs    dec_x1
       leax   8000-1,x
dec_x1 rts
***************************************
* cook     : computes base[index] + readbits(&in, bits[index])
* Input    : B = index
* Output   : D = base[index] + readbits(&in, bits[index])
* Modifies : D,X.
***************************************
cook   ldx    #biba
       abx
       aslb
       abx
       jsr    <getbix
       addd   1,x
       rts
       setdp  0